# ---------------
# Mock providers
# ---------------
mock_provider "aws" {
  # This is used because the mocked data is a random string instead of JSON, causing downstream dependencies to fail.
  # This block overrides the "json" output of ALL aws_iam_policy_document data blocks to use a JSON encoded string.
  mock_data "aws_iam_policy_document" {
    defaults = {
      json = "{\"some_fake\":\"json\"}"
    }
  }
  # This is used to return valid zone names for the us-west-2 zone instead of the random strings that Terraform will
  # generate for mocked tests. Not using this will cause the data block to create an empty list of strings.
  mock_data "aws_availability_zones" {
    defaults = {
      names = ["us-west-2a", "us-west-2b", "us-west-2c", "us-west-2d"]
    }
  }
}

# A mocked databricks provider is added here so that the alias requirement can be met
mock_provider "databricks" {
  alias = "mws"

  # This is used to return valid JSON for the data types of the assume role policy.
  mock_data "databricks_aws_assume_role_policy" {
    defaults = {
      json = "{\"some_fake\":\"json\"}"
    }    
  }

}
# ---------------

# ---------------
# Variables
# ---------------
# The variables block below provides variable values for the configuration in this directory (tf/aws/variables.tf).
# Note that the below values were generated by AI, and are not real values.

variables {
  # Required variables
  aws_account_id                = "123456789012"
  databricks_account_id         = "12345678-90ab-cdef-1234-567890abcdef"
  region                        = "us-west-2"
  resource_prefix               = "my-databricks-dev"
  admin_user                    = "workspace-admin@example.com"
  audit_log_delivery_exists     = false
  enable_security_analysis_tool = false
  metastore_exists              = false
  network_configuration         = "isolated"
  vpc_cidr_range                = "10.0.0.0/16"
  private_subnets_cidr          = ["10.0.1.0/24", "10.0.2.0/24"]
  privatelink_subnets_cidr      = ["10.0.3.0/24", "10.0.4.0/24"]
  sg_egress_ports               = ["443", "3306", "6666"]
  cmk_admin_arn                 = "arn:aws:iam::123456789012:role/DatabricksCMKAdmin"
  deployment_name               = "my-databricks-workspace"
  custom_vpc_id                 = null
  custom_private_subnet_ids     = null
  custom_sg_id                  = null
  custom_relay_vpce_id          = null
  custom_workspace_vpce_id      = null
  
  # New variables for GovCloud support
  databricks_gov_shard          = null
  aws_partition                 = null
  databricks_provider_host      = null
}

# ---------------
# Tests
# ---------------
# This runs a plan command on the module directly
run "plan_test" {
  command = plan
}
# ---------------
